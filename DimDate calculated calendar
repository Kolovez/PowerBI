_dimDate = 
VAR _MinYear = YEAR(TODAY())-4
VAR _MaxYear = YEAR(TODAY())+1
VAR _Dates = CALENDAR(DATE(_MinYear,1,1),DATE(_MaxYear,12,31))
VAR _StartWeek1 = DATE(2019,01,01)
RETURN
ADDCOLUMNS(
    _Dates,
    "Year", YEAR([Date]),
    "QuarterNo", FORMAT([Date], "\QQ"),
    "Quarter", FORMAT([Date], "\QQ"),
    "MonthNo", MONTH([Date]),
    "Month", FORMAT([Date], "MMM"),
    "Year-Month",YEAR([Date])&"-"&MONTH([Date]),
    "Year-MonthName",YEAR([Date])&"-"&FORMAT([Date], "MMM"),
    "Year-Quarter", YEAR([Date]) & "-" & FORMAT(QUARTER([Date]), "00"),
    "WeekdayNo", WEEKDAY([Date]),
    "Weekday", FORMAT([Date], "DDD"),
    "Week", WEEKNUM([Date],1),
    "WeekID", WEEKNUM([Date], 1) + (YEAR([Date]) - _MinYear) * 53,
    "WeekID_ISO", (INT([Date]- WEEKDAY([Date],3)-_StartWeek1)+7)/7,
    "Week_ISO", WEEKNUM([Date],21),
    "Year_ISO", 
        IF(
            WEEKNUM([Date], 21) < 5 && WEEKNUM([Date]) > 50,
            YEAR([Date])+1,
            IF(
                WEEKNUM([Date], 21) > 50 && WEEKNUM([Date]) < 5,
                YEAR([Date])-1,
                YEAR([Date])
            )
        ),
"RelativeDate", DATEDIFF(TODAY(), [Date], DAY),
"RelativeMonth", DATEDIFF(TODAY(), [Date], MONTH),
"RelativeYear", DATEDIFF(TODAY(), [Date], YEAR)
)
