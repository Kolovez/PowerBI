
Actual= SELECTEDMEASURE ()

Currrent Year Full= CALCULATE(
    SELECTEDMEASURE(),
    PARALLELPERIOD(
        _DimDate[Date],
        0,
        YEAR
    )
)

Last Year =
CALCULATE(
    SELECTEDMEASURE(),
    DATEADD(
        _DimDate[Date],
        -1,
        YEAR
    )
)

Last Year to Date=
CALCULATE(
    SELECTEDMEASURE(),
    SAMEPERIODLASTYEAR(_DimDate[Date]),
    'Time Intelligence'[Name] = "Year to Date"
)

Last Year to Date %=
VAR CurrentMeasure =
    CALCULATE( SELECTEDMEASURE(), DATESYTD( _DimDate[Date] ) )
VAR PriorMeasure =
    CALCULATE(
        SELECTEDMEASURE(),
        'Time Intelligence'[Name] = "Last Year to Date"
    )
VAR YearOverYearMeasure =
    IF(
        CurrentMeasure = BLANK()
            || PriorMeasure = BLANK(),
        BLANK(),
        CurrentMeasure - PriorMeasure
    )
RETURN
    DIVIDE( YearOverYearMeasure, PriorMeasure )


Month to Date=
CALCULATE( SELECTEDMEASURE( ), DATESMTD( _DimDate[Date] ) )

Previous Month=
CALCULATE(
    SELECTEDMEASURE(),
    DATEADD(
        _DimDate[Date],
        -1,
        MONTH
    )
)

Previous Year=
CALCULATE(
    SELECTEDMEASURE(),
    DATEADD(
        _DimDate[Date],
        -1,
        YEAR
    )
)

Previous Year Full=
CALCULATE(
    SELECTEDMEASURE(),
    PARALLELPERIOD(
       _DimDate[Date],
        -1,
        YEAR
    )
)

Quarter to Date=

CALCULATE(
    SELECTEDMEASURE(),
    DATESQTD( _DimDate[Date])
)

Rolling 12 months=
CALCULATE(
    CALCULATE(
        SELECTEDMEASURE(),
        DATESINPERIOD( _DimDate[Date], MAX( _DimDate[Date] ), -1, YEAR )
    ),
    DATEADD( _DimDate[Date], -1, MONTH )
)

Year over Year =
VAR CurrentMeasure =
    SELECTEDMEASURE()
VAR PriorMeasure =
    CALCULATE( SELECTEDMEASURE(), DATEADD( _DimDate[Date], -1, YEAR ) )
RETURN
    IF(
        CurrentMeasure = BLANK()
            || PriorMeasure = BLANK(),
        BLANK(),
        CurrentMeasure - PriorMeasure
    )
Year over Year %=
DIVIDE(
    CALCULATE( SELECTEDMEASURE( ), 'Time Intelligence'[Name] = "Year over Year" ),
    CALCULATE( SELECTEDMEASURE( ), DATEADD( _DimDate[Date], -1, YEAR ) )
)

Year to Date =

CALCULATE(
    SELECTEDMEASURE(),
    DATESYTD( _DimDate[Date] )
)
